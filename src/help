#!/usr/bin/env zsh

set -euo pipefail

local dir="$XDG_CONFIG_HOME/help"
local config_file="$dir/help.yml"

if [[ "${#argv[@]}" == 0 ]]; then
  local sources="$(yq '.sources | keys | join(", ")' $config_file)"
  echo "
help [[source] | <subcommand>]

  source: ${sources}

SUBCOMMANDS:

  with                    choose a source from a fuzzy finder
  add SOURCE COMMAND...   add a new source
  remove SOURCE           remove a source

INFO:

  Config file: $config_file
"
  exit
fi

case "$argv[1]" in
  add)
    export source="${argv[2]}"
    export action="${argv[3,-1]}"
    yq -i '.sources[env(source)] = env(action)' $config_file
    echo "Added $source"
    exit
    ;;

  remove)
    export source="${argv[2,-1]}"
    yq -i 'del(.sources[env(source)])' $config_file
    echo "Removed $source"
    exit
    ;;

  with)
    yq .sources $config_file | fzf | cut -d: -f2- | read action
    eval "$action"
    exit
    ;;

  *)
    export source="${(j: :)argv[@]}"
    local action="$(yq '.sources[env(source)]' $config_file)"

    if [[ "$action" == "null" ]]; then
      echo "$source is not a known help source."
      exit 1
    fi

    echo "Running '$action'"
    eval "$action"
    exit
esac
